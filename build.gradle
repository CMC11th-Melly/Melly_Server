plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'org.sonarqube' version '4.4.1.3373'
}

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"
    sourceCompatibility = project.javaVersion

    repositories {
        mavenCentral()
    }
}


sonar {
    properties {
        property "sonar.projectKey", "CMC11th-Melly_Melly_Server"
        property "sonar.organization", "cmc11th-melly"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'


    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    jacoco {
        toolVersion = '0.8.8'
    }

    jacocoTestReport {

        // jacoco report 형식 및 경로 설정
        reports {
            html.required = true
            csv.required = true
            xml.required = true
        }

        def Qdomains = []

        for (qPattern in '**/QA'..'**/QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
            Qdomains.add(qPattern + '*')
        }

        afterEvaluate {
            classDirectories.setFrom(
                    files(classDirectories.files.collect {
                        fileTree(dir: it, excludes: [] + Qdomains)
                    })
            )
        }

        finalizedBy 'jacocoTestCoverageVerification'
    }

    jacocoTestCoverageVerification {

        def Qdomains = []

        for (qPattern in '*.QA'..'*.QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
            Qdomains.add(qPattern + '*')
        }

        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                // includes = []

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.00
                }

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.00
                }

                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 200
                }

                excludes = [] + Qdomains
            }


        }
    }

    sonar {
        properties {
            property 'sonar.java.binaries', "${buildDir}/classes"
            property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
        }
    }

    dependencies {

        // Lombok
        implementation 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'


        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.17.2'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
        testImplementation "com.h2database:h2"

        implementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        implementation 'org.springframework.restdocs:spring-restdocs-restassured'
        implementation 'io.rest-assured:spring-mock-mvc'
    }

    bootJar.enabled = false
    jar.enabled = true

    test {
        useJUnitPlatform()
        finalizedBy 'jacocoTestReport' // test가 끝나면 jacocoTestReport를 실행합니다.
    }
}

// Sub-Module 적용
task copyPrivate(type: Copy) {
    copy {
        from './lib'
        include "*.yml", "*.json"
        into 'core/core-api/src/main/resources'
    }
}


